#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ДоступныеПсевдонимы = Справочники.лНастройкиЛогирования.ПолучитьМакет("ДоступныеПсевдонимыШаблона").ПолучитьТекст();
	
	// команды подстановки
	КомандыПодстановки = Новый Структура;
	КомандыПодстановки.Вставить("Уровень", "%УРОВЕНЬ%");
	КомандыПодстановки.Вставить("Событие", "%СОБЫТИЕ%");
	КомандыПодстановки.Вставить("Сообщение", "%СООБЩЕНИЕ%");
	КомандыПодстановки.Вставить("Дата", "%ДАТА%");
	КомандыПодстановки.Вставить("ДатаМС", "%ДАТАМС%");
	КомандыПодстановки.Вставить("УниверсальнаяДатаМС", "%УНИВЕРСАЛЬНАЯДАТАМС%");

	Для Каждого ОписаниеКоманды Из КомандыПодстановки Цикл

		ИдентификаторКоманды = "Вставить_" + ОписаниеКоманды.Ключ;
		НоваяКоманда = Команды.Добавить(ИдентификаторКоманды);
		НоваяКоманда.Действие = "Подключаемый_ВставитьПсевдоним";

		НоваяКнопка = Элементы.Вставить(ИдентификаторКоманды, Тип("КнопкаФормы"), Элементы.КомандыВставить);
		НоваяКнопка.Заголовок = ОписаниеКоманды.Значение;
		НоваяКнопка.ИмяКоманды = ИдентификаторКоманды;

	КонецЦикла;

	Если Параметры.Свойство("ТекШаблон") Тогда
		Шаблон = Параметры.ТекШаблон;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)

	ОповеститьОВыборе(Шаблон);

КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВставитьПсевдоним(Команда)

	НачалоСтроки = 0;
	НачалоКолонки = 0;
	КонецСтроки = 0;
	КонецКолонки = 0;

	Элементы.Шаблон.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);

	ВыделенныйТекст = Элементы.Шаблон.ВыделенныйТекст;
	ТекстРедактирования = Элементы.Шаблон.ТекстРедактирования;

	НовыйТекст = СтрЗаменить(ТекстРедактирования, ВыделенныйТекст, "");

	ТекстДо = "";
	ТекстПосле = "";
	
	Для НомерСтроки = 1 По СтрЧислоСтрок(НовыйТекст) Цикл
		ТекстСтроки = СтрПолучитьСтроку(НовыйТекст, НомерСтроки);
		Если НомерСтроки < НачалоСтроки Тогда
			ТекстДо = ТекстДо + ТекстСтроки + Символы.ПС;
		ИначеЕсли НомерСтроки = НачалоСтроки Тогда
			Если НачалоКолонки > 1 Тогда
				ТекстДо = ТекстДо + Лев(ТекстСтроки, НачалоКолонки - 1);
				ТекстПосле = Сред(ТекстСтроки, НачалоКолонки) + Символы.ПС;
			Иначе
				ТекстПосле = ТекстСтроки;
			КонецЕсли;
		Иначе
			ТекстПосле = ТекстПосле + ТекстСтроки + Символы.ПС;
		КонецЕсли;
	КонецЦикла;

	НовыйТекст = ТекстДо + Элементы[Команда.Имя].Заголовок + ТекстПосле;
	НовыйТекст = СтрЗаменить(НовыйТекст, Символы.ПС, "");

	Шаблон = СокрЛП(НовыйТекст);

КонецПроцедуры

#КонецОбласти