#Область ПрограммныйИнтерфейс

Функция НовыйЛог(ИмяЛога = "", Уровень = "") Экспорт
	
	ИмяЛогаДляСоздания = ИмяЛога;
	Если Не ЗначениеЗаполнено(ИмяЛогаДляСоздания) Тогда
		ИмяЛогаДляСоздания = ИмяЛогаПоУмолчанию();
	КонецЕсли;
	
	ПараметрыЛога = НовыйПустойЛог(ИмяЛогаДляСоздания);
	Если ЗначениеЗаполнено(Уровень) Тогда
		ПараметрыЛога.Уровень = Уровень;
	КонецЕсли;	
	
	#Если Клиент Тогда
	ЦПР_Л_ПараметрыСеанса.Логи.Вставить(ИмяЛогаДляСоздания, ПараметрыЛога);
	#КонецЕсли
	лЛогСлужебныйСервер.ДобавитьТекущийЛогНаСервер(ИмяЛогаДляСоздания, ПараметрыЛога);
	Возврат ПараметрыЛога;
КонецФункции

Процедура ДобавитьСпособЛогирования(Способ, Параметры, ЛогИлиИмяЛога) Экспорт
	ТекущийЛог = ТекущийЛог(ЛогИлиИмяЛога);
	Если ТекущийЛог = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущийСпособЛогирования = ТекущийСпособЛогирования(Способ);
	Если ТекущийСпособЛогирования = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ТекущийСпособЛогирования.ЗапретДублейУЛога И ЕстьСпособЛогированияДляЛога(ТекущийЛог,
		ТекущийСпособЛогирования) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавляемыйСпособ = Новый Структура;
	ДобавляемыйСпособ.Вставить("Способ", ТекущийСпособЛогирования);
	ДобавляемыйСпособ.Вставить("Параметры", Параметры);
	
	ТекущийЛог.Способы.Добавить(ДобавляемыйСпособ);	
	
	лЛогСлужебныйСервер.ДобавитьТекущийЛогНаСервер(ТекущийЛог.Имя, ТекущийЛог);
КонецПроцедуры

Процедура ЗаписатьНовоеСообщениеВЛог(Сообщение, УровеньЛогирования, ЛогИлиИмяЛога) Экспорт
	ТекущийЛог = ТекущийЛог(ЛогИлиИмяЛога);
	Если ТекущийЛог = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ВозможнаЗаписьЛога(ТекущийЛог, УровеньЛогирования) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекСпособ ИЗ ТекущийЛог.Способы Цикл
		ЗаписатьЛогПоСпособу(ТекущийЛог, ТекСпособ.Способ, ТекСпособ.Параметры, УровеньЛогирования, Сообщение);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаписатьЛогПоСпособу(Лог, СпособЛогирования, Параметры, УровеньЛогирования, Сообщение) Экспорт
	Если СпособЛогирования.ЗаписьНаСервере Тогда
		лЛогСлужебныйСервер.ВыполнитьЗаписьЛогаПоСпособу(Лог, СпособЛогирования, Параметры, УровеньЛогирования, Сообщение);
	Иначе
		ВыполнитьЗаписьЛогаПоСпособу(Лог, СпособЛогирования, Параметры, УровеньЛогирования, Сообщение);
	КонецЕсли;
КонецПроцедуры

Функция ПараметрыЛогирования() Экспорт
	ПараметрыЛогирования = Новый Структура;
	ПараметрыЛогирования.Вставить("Уровень", УровниЛогирования().Информация);
	ПараметрыЛогирования.Вставить("Способы", Новый Массив);
//	ПараметрыЛогирования.Вставить("", "");
	
	Возврат ПараметрыЛогирования;
КонецФункции

Функция СпособыЛогирования() Экспорт
	Способы = Новый Структура;
	Способы.Вставить("Консоль", НовыйСпособЛогирования("Консоль"));
	Способы.Вставить("ЖурналРегистрации", НовыйСпособЛогирования("ЖурналРегистрации",Истина));
	Способы.Вставить("Память", НовыйСпособЛогирования("Память"));
	Способы.Вставить("Файл", НовыйСпособЛогирования("Файл",,Ложь));
	Способы.Вставить("База", НовыйСпособЛогирования("База",Истина));
	
	лЛогПереопределяемый.ПриДобавленииСпособовЛогирования(Способы);
	
	Возврат Способы;
КонецФункции

Функция УровниЛогирования() Экспорт
	Уровни = Новый Структура;
	Уровни.Вставить("Нет", НовыйУровеньЛогирования("Нет", 999));
	Уровни.Вставить("Отладка", НовыйУровеньЛогирования("Отладка", 0));
	Уровни.Вставить("Информация", НовыйУровеньЛогирования("Информация", 1));
	Уровни.Вставить("Предупреждение", НовыйУровеньЛогирования("Предупреждение", 2));
	Уровни.Вставить("Ошибка", НовыйУровеньЛогирования("Ошибка", 3));
	Уровни.Вставить("КритическаяОшибка", НовыйУровеньЛогирования("КритическаяОшибка", 4));
	
	Возврат Уровни;
КонецФункции

Функция ПараметрыСпособаЛогирования(СпособИлиИмяСпособа) Экспорт
	ТекущийСпособ = ТекущийСпособЛогирования(СпособИлиИмяСпособа);
	Если ТекущийСпособ = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СпособыЛогирования = СпособыЛогирования();
	Если ТекущийСпособ.Имя = СпособыЛогирования.База.Имя Тогда
		Возврат Неопределено;
	ИначеЕсли ТекущийСпособ.Имя = СпособыЛогирования.Консоль.Имя Тогда 
		Возврат Неопределено;
	ИначеЕсли ТекущийСпособ.Имя = СпособыЛогирования.ЖурналРегистрации.Имя Тогда 
		Возврат Неопределено;
	ИначеЕсли ТекущийСпособ.Имя = СпособыЛогирования.Память.Имя Тогда 
		Возврат Неопределено;
	ИначеЕсли ТекущийСпособ.Имя = СпособыЛогирования.Файл.Имя Тогда 
		ПараметрыСпособа = Новый Структура;
		ПараметрыСпособа.Вставить("ИмяФайла", "");
		
		Возврат ПараметрыСпособа;
	КонецЕсли;
КонецФункции 

Процедура ОтключитьЛог(ЛогИлиИмяЛога = Неопределено) Экспорт
	ТекущийЛог = ТекущийЛог(ЛогИлиИмяЛога);
	Если ТекущийЛог = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийЛог.Использование = Ложь;
	лЛогСлужебныйСервер.ДобавитьТекущийЛогНаСервер(ТекущийЛог.Имя, ТекущийЛог);
	
КонецПроцедуры

Процедура ВключитьЛог(ЛогИлиИмяЛога = Неопределено) Экспорт
	ТекущийЛог = ТекущийЛог(ЛогИлиИмяЛога);
	Если ТекущийЛог = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийЛог.Использование = Истина;
	лЛогСлужебныйСервер.ДобавитьТекущийЛогНаСервер(ТекущийЛог.Имя, ТекущийЛог);
	
КонецПроцедуры

Процедура УстановитьУровеньЛогирования(Уровень, ЛогИлиИмяЛога = Неопределено) Экспорт
	ТекущийЛог = ТекущийЛог(ЛогИлиИмяЛога);
	Если ТекущийЛог = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Уровень) = Тип("Строка") Тогда
		ТекущийЛог.Уровень = УровниЛогирования()[Уровень];
	Иначе
		ТекущийЛог.Уровень = Уровень;
	КонецЕсли;
	лЛогСлужебныйСервер.ДобавитьТекущийЛогНаСервер(ТекущийЛог.Имя, ТекущийЛог);
	
КонецПроцедуры

Функция ДанныеЛога(ЛогИлиИмяЛога = Неопределено) Экспорт
	ТекущийЛог = ТекущийЛог(ЛогИлиИмяЛога);
	Если ТекущийЛог = Неопределено Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Возврат ТекущийЛог.ДанныеСессии;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ВыполнитьЗаписьЛогаПоСпособу(Лог, СпособЛогирования, Параметры, УровеньЛогирования, Сообщение) Экспорт
	
	СообщениеДляЗаписи = СтрокаСообщенияЛога(Сообщение, УровеньЛогирования, Лог, СпособЛогирования);
	
	Способы = СпособыЛогирования();
	Если СпособЛогирования.Имя = Способы.Консоль.Имя Тогда
		ВывестиСообщениеВКонсоль(Лог, СообщениеДляЗаписи, УровеньЛогирования);
	ИначеЕсли СпособЛогирования.Имя = Способы.ЖурналРегистрации.Имя Тогда 
		лЛогСлужебныйСервер.ВывестиСообщениеВЖурналРегистрации(Лог, СообщениеДляЗаписи, УровеньЛогирования);
	ИначеЕсли СпособЛогирования.Имя = Способы.База.Имя Тогда 
		лЛогСлужебныйСервер.ЗаписатьСообщениеЛогаВБазу(Лог, СообщениеДляЗаписи, УровеньЛогирования);
	ИначеЕсли СпособЛогирования.Имя = Способы.Память.Имя Тогда 
		ВывестиСообщениеВПамять(Лог, СообщениеДляЗаписи, УровеньЛогирования);
	ИначеЕсли СпособЛогирования.Имя = Способы.Файл.Имя Тогда 
		ВывестиСообщениеВФайл(Лог, СообщениеДляЗаписи, УровеньЛогирования, Параметры);
	Иначе
//		лЛогПереопределяемый.
	КонецЕсли;
	
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыводСообщенийВПредопределенныеКаналыЛогирования

Процедура ВывестиСообщениеВКонсоль(Лог, Сообщение, УровеньЛогирования)
	УровниЛогирования = УровниЛогирования();
	
	Если УровеньЛогирования.Имя = УровниЛогирования.Информация.Имя Тогда
		Статус = СтатусСообщения.Информация;
	ИначеЕсли УровеньЛогирования.Имя = УровниЛогирования.Предупреждение.Имя Тогда 
		Статус = СтатусСообщения.Внимание;
	ИначеЕсли УровеньЛогирования.Имя = УровниЛогирования.Ошибка.Имя Тогда 
		Статус = СтатусСообщения.Важное;
	ИначеЕсли УровеньЛогирования.Имя = УровниЛогирования.КритическаяОшибка.Имя Тогда 
		Статус = СтатусСообщения.ОченьВажное;
	Иначе 
		Статус = СтатусСообщения.Обычное;
	КонецЕсли;
	
	Сообщить(Сообщение,Статус);
	
КонецПроцедуры

Процедура ВывестиСообщениеВПамять(Лог, Сообщение, УровеньЛогирования)
	Лог.ДанныеСессии.Добавить(Сообщение);	
КонецПроцедуры

Процедура ВывестиСообщениеВФайл(Лог, Сообщение, УровеньЛогирования, ПараметрыВывода)
	ЗаписьТекста = Новый ЗаписьДанных(ПараметрыВывода.ИмяФайла,,,,Истина);
	ЗаписьТекста.ЗаписатьСтроку(Сообщение);
КонецПроцедуры

#КонецОбласти

Функция СтрокаСообщенияЛога(Сообщение, УровеньЛогирования, Лог, СпособЛогирования) Экспорт
	
	ДатаСообщенияУниверсальнаяМС = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ДатаСообщения				 = МестноеВремя('00010101'+ Цел(ДатаСообщенияУниверсальнаяМС/1000));
	ДатаМиллисекунды             = ДатаСообщенияУниверсальнаяМС % 1000; 
	ДатаПредставление            = Формат(ДатаСообщения, Лог.ФорматДаты);	
	ДатаПредставлениеМС          = Формат(ДатаМиллисекунды, "ЧЦ=3; ЧДЦ=0; ЧВН=; ЧГ=");
	
	СтрокаСообщения = Лог.Шаблон;
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%ДАТА%",                 ДатаПредставление);
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%ДАТАМС%",               ДатаПредставлениеМС);
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%УНИВЕРСАЛЬНАЯДАТАМС%",  Формат(ДатаСообщенияУниверсальнаяМС, "ЧГ="));
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%УРОВЕНЬ%",              Врег(УровеньЛогирования.Имя));
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%СООБЩЕНИЕ%",            Сообщение);
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%СОБЫТИЕ%",              Лог.ИмяСобытия);
		
	Возврат СтрокаСообщения;
	
КонецФункции

Функция ЕстьСпособЛогированияДляЛога(Лог, СпособЛогирования)
	Для Каждого ТекСпособ Из Лог.Способы Цикл
		Если ТекСпособ.Способ.Имя = СпособЛогирования.Имя Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат Ложь;
КонецФункции

Функция ТекущийЛог(ЛогИлиИмяЛога)
	Если ЛогИлиИмяЛога = Неопределено Или Не ЗначениеЗаполнено(ЛогИлиИмяЛога) Тогда
		Возврат ЛогПоИмени(ИмяЛогаПоУмолчанию());
	ИначеЕсли ТипЗнч(ЛогИлиИмяЛога) = Тип("Строка") Тогда
		Возврат ЛогПоИмени(ЛогИлиИмяЛога);
	ИначеЕсли ТипЗнч(ЛогИлиИмяЛога) = Тип("Структура") Тогда
		Возврат ЛогИлиИмяЛога;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Функция ТекущийСпособЛогирования(СпособИлиИмяСпособа)
	Если ТипЗнч(СпособИлиИмяСпособа) = Тип("Строка") Тогда
		Способы = СпособыЛогирования();
		Возврат Способы[СпособИлиИмяСпособа];
	ИначеЕсли ТипЗнч(СпособИлиИмяСпособа) = Тип("Структура") Тогда
		Возврат СпособИлиИмяСпособа;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Функция ЛогПоИмени(ИмяЛога)
	#Если Клиент Тогда
	Возврат ЦПР_Л_ПараметрыСеанса.Логи[ИмяЛога];
	#Иначе
	Возврат лЛогСлужебныйСервер.ЛогПоИмениНаСервере(ИмяЛога);
	#КонецЕсли
КонецФункции

Функция НовыйПустойЛог(ИмяЛога) 
	Настройка = Новый Структура;
	Настройка.Вставить("Имя", ИмяЛога);
	Настройка.Вставить("Способы", Новый Массив);
	Настройка.Вставить("Уровень", УровниЛогирования().Информация);
	Настройка.Вставить("ДанныеСессии", Новый Массив);
	Настройка.Вставить("Использование", Истина);
	Настройка.Вставить("ИмяСобытия", "Логирование");
	Настройка.Вставить("Шаблон", "%ДАТА% - %УРОВЕНЬ% - %СООБЩЕНИЕ%");
	Настройка.Вставить("ФорматДаты", "ДФ='yyyy.MM.dd HH:mm:ss'");
	#Если Клиент Тогда
		Настройка.Вставить("НомерСеанса", ЦПР_Л_ПараметрыСеанса.НомерСеанса);
	#Иначе
		Настройка.Вставить("НомерСеанса", лЛогСлужебныйСервер.НомерСеанса());
	#КонецЕсли
	#Если МобильноеПриложениеКлиент Тогда
	Настройка.Вставить("", лЛогСлужебныйСервер.ИмяТекущегоПользователяБазы());
	#Иначе
	Настройка.Вставить("ИмяПользователя", ИмяПользователя());
	#КонецЕсли
	
	Возврат Настройка;
КонецФункции

Функция ВозможнаЗаписьЛога(Лог, Уровень)
	Если Не Лог.Использование Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Лог.Уровень.Приоритет> Уровень.Приоритет Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Функция НовыйУровеньЛогирования(Имя, Приоритет) 
	Уровень = Новый Структура;
	Уровень.Вставить("Имя", Имя);
	Уровень.Вставить("Приоритет", Приоритет);
	
	Возврат Уровень;
КонецФункции

Функция НовыйСпособЛогирования(Имя ="", ЗаписьНаСервере = Ложь, ЗапретДублейУЛога = Истина) 
	Способ = Новый Структура;
	Способ.Вставить("Имя",Имя);
	Способ.Вставить("ЗаписьНаСервере", ЗаписьНаСервере);
	Способ.Вставить("ЗапретДублейУЛога", ЗапретДублейУЛога);
	
	Возврат Способ;
КонецФункции

Функция ИмяЛогаПоУмолчанию()
	Возврат "logFor1C";
КонецФункции

#КонецОбласти
